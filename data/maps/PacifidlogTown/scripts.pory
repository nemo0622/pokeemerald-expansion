raw `
PacifidlogTown_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, PacifidlogTown_OnTransition
	map_script MAP_SCRIPT_ON_RESUME, PacifidlogTown_OnResume
	map_script MAP_SCRIPT_ON_FRAME_TABLE, PacifidlogTown_OnFrame
	.byte 0

PacifidlogTown_OnFrame:
	map_script_2 VAR_PACIFIDLOG_TOWN_STATE, 1, PacifidlogTown_EventScript_ExitDivingMinigame
	map_script_2 VAR_PACIFIDLOG_TOWN_STATE, 2, PacifidlogTown_EventScript_ExitDivingMinigameAllPokemonFainted
	.2byte 0

PacifidlogTown_OnTransition:
	setflag FLAG_VISITED_PACIFIDLOG_TOWN
	call_if_eq VAR_PACIFIDLOG_TOWN_STATE, 1, PacifidlogTown_SetCompetitorPositionsAfterMinigame
	call_if_eq VAR_PACIFIDLOG_TOWN_STATE, 2, PacifidlogTown_ResetCompetitorFlagsFailsafe
	end

PacifidlogTown_OnResume:
	setstepcallback STEP_CB_PACIFIDLOG_BRIDGE
	end

PacifidlogTown_EventScript_NinjaBoy::
	msgbox PacifidlogTown_Text_NeatHousesOnWater, MSGBOX_NPC
	end

PacifidlogTown_EventScript_Girl::
	msgbox PacifidlogTown_Text_FastRunningCurrent, MSGBOX_NPC
	end

PacifidlogTown_EventScript_Fisherman::
	msgbox PacifidlogTown_Text_SkyPillarTooScary, MSGBOX_NPC
	end

PacifidlogTown_EventScript_TownSign::
	msgbox PacifidlogTown_Text_TownSign, MSGBOX_SIGN
	end

PacifidlogTown_Text_FastRunningCurrent:
	.string "The sea between PACIFIDLOG and\n"
	.string "SLATEPORT has a fast-running tide.\p"
	.string "If you decide to SURF, you could end\n"
	.string "up swept away somewhere else.$"

PacifidlogTown_Text_NeatHousesOnWater:
	.string "See, isn't it neat?\n"
	.string "These houses are on water!\p"
	.string "I was born here!$"

PacifidlogTown_Text_SkyPillarTooScary:
	.string "The SKY PILLAR?\p"
	.string "…Oh, you must mean that tall, tall\n"
	.string "tower a little further out.\p"
	.string "If you asked me, I wouldn't climb it.\n"
	.string "It's too scary to get up that high.\p"
	.string "Life at sea level in PACIFIDLOG,\n"
	.string "that suits me fine.$"

PacifidlogTown_Text_TownSign:
	.string "PACIFIDLOG TOWN\p"
	.string "“Where the morning sun smiles upon\n"
	.string "the waters.”$"

`

script PacifidlogTown_EventScript_Kirlia
{
	lock
	faceplayer
	msgbox(format("KIRLIA: Shaaah!"))
	closemessage
	release
	end
}

script PacifidlogTown_EventScript_TeleportMan
{
	lock
	faceplayer
	msgbox(format("I host a TELEPORT service to and from SLATEPORT CITY! Would you like a ride?"), MSGBOX_YESNO)

	if(var(VAR_RESULT) == 0) // no
	{
		msgbox(format("Come back when you want to TELEPORT!"))
		closemessage
		release
		end
	}
	else
	{
		msgbox(format("Alright, here we go!"))
		closemessage
		warp(MAP_SLATEPORT_CITY, 20, 21)
		waitstate
		release
		end
	}
	release
	end
}

script PacifidlogTown_EventScript_AppleSalesman
{
	lock
	faceplayer
	message(format("Apples for sale! Come get your apples, fresh from the mainland!"))
	waitmessage
	pokemart(PacifidlogTown_AppleMart)
	msgbox(format("Come back soon!"))
	closemessage
	release
	end
}

mart PacifidlogTown_AppleMart
{
	ITEM_SWEET_APPLE
	ITEM_TART_APPLE
	ITEM_SYRUPY_APPLE
	ITEM_FRESH_START_MOCHI
}

script PacifidlogTown_EventScript_MinigameParticipant_LittleBoy
{
	lock
	faceplayer
	msgbox(format("RUA: I can't wait for the next SCUBA SAFARI round! I've got a plan to win!"))
	closemessage
	applymovement(11, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}
script PacifidlogTown_EventScript_MinigameParticipant_LittleGirl
{
	lock
	faceplayer
	msgbox(format("PORA: My family moved here to scuba dive around the CORSOLA colonies nearby!\p I love watching the SCUBA SAFARI!"))
	closemessage
	applymovement(10, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}
script PacifidlogTown_EventScript_MinigameParticipant_TriatheleteM
{
	lock
	faceplayer
	msgbox(format("TAONGA: Huff huff... Gotta train...\N PACIFIDLOG TOWN honors it's seafaring roots through the SCUBA SAFARI competition! I need to do my best!"))
	closemessage
	applymovement(7, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}
script PacifidlogTown_EventScript_MinigameParticipant_TriatheleteF
{
	lock
	faceplayer
	msgbox(format("TAIKO: Huff huff... Have to train...\N I have to swim fast to find rare POKéMON in the competition, they'll get a higher score!"))
	closemessage
	applymovement(6, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

// DIVING MINIGAME UNDERWATER SCRIPTS

script DivingMinigame_EventScript_CentralSign
{
	lockall
	msgbox(format("PACIFIDLOG SCUBA SAFARI AREA 1\n CENTRAL AREA\p An Introduction to Diving!"))
	closemessage
	releaseall
	end
}
script DivingMinigame_EventScript_NorthSign
{
	lockall
	msgbox(format("PACIFIDLOG SCUBA SAFARI AREA 2\n NORTH AREA\p Cool Temperatures, Cooler POKéMON!"))
	closemessage
	releaseall
	end
}
script DivingMinigame_EventScript_EastSign
{
	lockall
	msgbox(format("PACIFIDLOG SCUBA SAFARI AREA 3\n EAST AREA\p Mysterious POKéMON in Mysterious Ruins!"))
	closemessage
	releaseall
	end
}
script DivingMinigame_EventScript_SouthSign
{
	lockall
	msgbox(format("PACIFIDLOG SCUBA SAFARI AREA 4\n SOUTH AREA\p The Depths of the Seas!"))
	closemessage
	releaseall
	end
}
script DivingMinigame_EventScript_WestSign
{
	lockall
	msgbox(format("PACIFIDLOG SCUBA SAFARI AREA 5\n WEST AREA\p Shallow Pools where Land meets Sea!"))
	closemessage
	releaseall
	end
}

script DivingMinigame_EventScript_SailorJudge
{
	lock
	faceplayer
	msgbox(format("Yo ho ho, SCUBA SAFARI master! Are you ready to end your SCUBA SAFARI?"), MSGBOX_YESNO)

	if(var(VAR_RESULT) == 0) //no
	{
		buffernumberstring(STR_VAR_1, VAR_DIVING_MINIGAME_STEP_COUNT)
		msgbox(format("Great, get back to it!\n You have {STR_VAR_1} tiles left to swim before time's up!"))
		closemessage
	}
	else
	{
		msgbox(format("Alrighty here, let me start the announcement!"))
		closemessage
		delay(60)
		goto(DivingMinigame_EventScript_TimesUp)
	}

	applymovement(1, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

// DIVING MINIGAME PACIFIDLOG SCRIPTS

script PacifidlogTown_EventScript_DivingMinigameNPC
{
	lock
	faceplayer
	showmoneybox(0, 0)
	message(format("Ahoy, TRAINER! Do you want to play the PACIFIDLOG SCUBA SAFARI? Just ¥100!"))
	waitmessage

	multichoice(22, 6, MULTI_YESNOINFO, TRUE)

	if(var(VAR_RESULT) == 1) // no
	{
		msgbox(format("Alright! Come back soon!"))
		closemessage
		hidemoneybox
		release
		end
	}
	elif(var(VAR_RESULT) == 0) // yes
	{
		checkmoney(100)
		if(var(VAR_RESULT) == 0) // not enough money
		{
			msgbox(format("Sorry, sailor! Looks like you're a bit low on funds."))
			closemessage
			hidemoneybox
			release
			end
		}

		playse(SE_SHOP)
		removemoney(100)
		updatemoneybox
		delay(50)
		hidemoneybox
		delay(10)

		msgbox(format("Alrighty! Let's get a SCUBA GROUP together!"))
		closemessage

		fadescreen(FADE_TO_BLACK)
		delay(60)

		// Set Scuba Safari variables and stuff
		setflag(FLAG_DOING_DIVING_MINIGAME)
		clearflag(FLAG_NEW_DIVING_GAME_HIGH_SCORE)
		special(EnterDivingMinigame)

		// Reset competitor flags
		clearflag(FLAG_SCUBA_COMPETITOR_1)
		clearflag(FLAG_SCUBA_COMPETITOR_2)
		clearflag(FLAG_SCUBA_COMPETITOR_3)
		clearflag(FLAG_SCUBA_COMPETITOR_4)

		// Hide all NPCs throughout water, so they can be shown instead in the intro
		setflag(FLAG_HIDE_COMPETITOR_1_UNDERWATER)
		setflag(FLAG_HIDE_COMPETITOR_2_UNDERWATER)
		setflag(FLAG_HIDE_COMPETITOR_3_UNDERWATER)
		setflag(FLAG_HIDE_COMPETITOR_4_UNDERWATER)

		// Hides all competitors in the intro by default, they're enabled further below
		setflag(FLAG_HIDE_COMPETITOR_1_UNDERWATER_INTRO)
    	setflag(FLAG_HIDE_COMPETITOR_2_UNDERWATER_INTRO)
    	setflag(FLAG_HIDE_COMPETITOR_3_UNDERWATER_INTRO)
    	setflag(FLAG_HIDE_COMPETITOR_4_UNDERWATER_INTRO)

		// Decide which NPCs will be participating - always 1 boy and 1 girl for now
		// Boys - 50% chance Taonga (Triathlete M), 50% chance Rua (Youngster)
		randompercentage(50)
		if(var(VAR_RESULT) == TRUE) // Taonga chosen
		{
			setflag(FLAG_SCUBA_COMPETITOR_3) // set competitor flag
			clearflag(FLAG_HIDE_COMPETITOR_3_UNDERWATER_INTRO) // show in intro
			setobjectxy(7, 23, 14)
			applymovement(7, Common_Movement_FaceUp)
		}
		else
		{
			setflag(FLAG_SCUBA_COMPETITOR_4) // set competitor flag
			clearflag(FLAG_HIDE_COMPETITOR_4_UNDERWATER_INTRO) // show in intro
			setobjectxy(11, 23, 14)
			applymovement(11, Common_Movement_FaceUp)
		}
		// Girls - 50% chance Taiko (Triathlete F), 50% chance Pora (Little Girl)
		randompercentage(50)
		if(var(VAR_RESULT) == TRUE) // Taiko chosen
		{
			setflag(FLAG_SCUBA_COMPETITOR_2) // set competitor flag
			clearflag(FLAG_HIDE_COMPETITOR_2_UNDERWATER_INTRO) // show in intro
			setobjectxy(6, 25, 14)
			applymovement(6, Common_Movement_FaceUp)
		}
		else
		{
			setflag(FLAG_SCUBA_COMPETITOR_1) // set competitor flag
			clearflag(FLAG_HIDE_COMPETITOR_1_UNDERWATER_INTRO) // show in intro
			setobjectxy(10, 25, 14)
			applymovement(10, Common_Movement_FaceUp)
		}

		fadescreen(FADE_FROM_BLACK)
		delay(20)

		msgbox(format("Looks like we've got a good crew going! Let's DIVE!"))
		closemessage

		applymovement(OBJ_EVENT_ID_PLAYER, PacifidlogTown_Movement_CompetitorsExcited)
		if(flag(FLAG_SCUBA_COMPETITOR_1))
		{
			applymovement(10, PacifidlogTown_Movement_CompetitorsExcited)
			setflag(FLAG_HIDE_COMPETITOR_1_PACIFIDLOG)
		}
		if(flag(FLAG_SCUBA_COMPETITOR_2))
		{
			applymovement(6, PacifidlogTown_Movement_CompetitorsExcited)
			setflag(FLAG_HIDE_COMPETITOR_2_PACIFIDLOG)
		}
		if(flag(FLAG_SCUBA_COMPETITOR_3))
		{
			applymovement(7, PacifidlogTown_Movement_CompetitorsExcited)
			setflag(FLAG_HIDE_COMPETITOR_3_PACIFIDLOG)
		}
		if(flag(FLAG_SCUBA_COMPETITOR_4))
		{
			applymovement(11, PacifidlogTown_Movement_CompetitorsExcited)
			setflag(FLAG_HIDE_COMPETITOR_4_PACIFIDLOG)
		}

		waitmovement(0)
		delay(30)

		warp(MAP_DIVING_MINIGAME, 4, 14)
		waitstate
		end
	}
	else // info
	{
		hidemoneybox
		delay(10)
		msgbox(format("Ehehe, interested in the SCUBA SAFARI? Welcome! This is the pride and joy of PACIFIDLOG TOWN!\p The gist of the SCUBA SAFARI is DIVING down here and trying to catch the coolest, rarest POKéMON you can in a limited time. You can even find some cool treasures if you're lucky!\p There's a few basic rules. First, when you're down there, you only have {COLOR GREEN}200 tiles of movement{COLOR DARK_GRAY} or {COLOR GREEN}five encounters{COLOR DARK_GRAY} to explore. After that, your POKéMON is scored based on level, stats, and rarity. You'll have a few competitors down there, so do your best to catch something cool!\p And remember, only the {COLOR RED}most recent{COLOR DARK_GRAY} POKéMON you catch counts for scoring."))
		closemessage
		msgbox(format("...Phew, that was a lot! Oh right, and you don't even need the DIVE HM to play! We provide you with everything you need. Now, with that out of the way..."))
		closemessage
		goto(PacifidlogTown_EventScript_DivingMinigameNPC)
	}

	release
	end
}

movement PacifidlogTown_Movement_CompetitorsExcited
{
	jump_in_place_up * 2
	walk_in_place_up * 2
	step_end
}

movement PacifidlogTown_Movement_CompetitorsOutOfWater
{
	jump_down * 2
	face_up
	step_end
}

movement PacifidlogTown_Movement_JudgeChecksScoreLeft
{
	walk_left
	walk_in_place_down
	delay_16
	emote_heart
	delay_16
}
movement PacifidlogTown_Movement_JudgeChecksScoreRight
{
	walk_right * 2
	walk_in_place_down
	delay_16
	emote_heart
	delay_16
}
movement PacifidlogTown_Movement_JudgeChecksScorePlayer
{
	walk_left
	walk_in_place_down
	delay_16
	emote_heart
	delay_16
	step_end
}

script PacifidlogTown_ResetCompetitorFlagsFailsafe
{
	clearflag(FLAG_HIDE_COMPETITOR_1_PACIFIDLOG)
	clearflag(FLAG_HIDE_COMPETITOR_2_PACIFIDLOG)
	clearflag(FLAG_HIDE_COMPETITOR_3_PACIFIDLOG)
	clearflag(FLAG_HIDE_COMPETITOR_4_PACIFIDLOG)
	showobjectat(10, MAP_PACIFIDLOG_TOWN)
	showobjectat(6, MAP_PACIFIDLOG_TOWN)
	showobjectat(7, MAP_PACIFIDLOG_TOWN)
	showobjectat(11, MAP_PACIFIDLOG_TOWN)
}

script PacifidlogTown_SetCompetitorPositionsAfterMinigame
{
	clearflag(FLAG_HIDE_COMPETITOR_1_PACIFIDLOG)
	clearflag(FLAG_HIDE_COMPETITOR_2_PACIFIDLOG)
	clearflag(FLAG_HIDE_COMPETITOR_3_PACIFIDLOG)
	clearflag(FLAG_HIDE_COMPETITOR_4_PACIFIDLOG)
	showobjectat(10, MAP_PACIFIDLOG_TOWN)
	showobjectat(6, MAP_PACIFIDLOG_TOWN)
	showobjectat(7, MAP_PACIFIDLOG_TOWN)
	showobjectat(11, MAP_PACIFIDLOG_TOWN)

	if(flag(FLAG_SCUBA_COMPETITOR_1))
	{
		setobjectxyperm(10, 25, 12)
		applymovement(10, Common_Movement_WalkUp2)
		setobjectmovementtype(10, MOVEMENT_TYPE_FACE_DOWN)
	}
	if(flag(FLAG_SCUBA_COMPETITOR_2))
	{
		setobjectxyperm(6, 25, 12)
		applymovement(6, Common_Movement_WalkUp2)
		setobjectmovementtype(6, MOVEMENT_TYPE_FACE_DOWN)
	}
	if(flag(FLAG_SCUBA_COMPETITOR_3))
	{
		setobjectxyperm(7, 23, 12)
		applymovement(7, Common_Movement_WalkUp2)
		setobjectmovementtype(7, MOVEMENT_TYPE_FACE_DOWN)
	}
	if(flag(FLAG_SCUBA_COMPETITOR_4))
	{
		setobjectxyperm(11, 23, 12)
		applymovement(11, Common_Movement_WalkUp2)
		setobjectmovementtype(11, MOVEMENT_TYPE_FACE_DOWN)
	}
}

script PacifidlogTown_EventScript_ExitDivingMinigame
{
	lockall
	turnplayer(DIR_NORTH)

	// Competitors splash out of the water
	if(flag(FLAG_SCUBA_COMPETITOR_1))
	{
		applymovement(10, PacifidlogTown_Movement_CompetitorsOutOfWater)
	}
	if(flag(FLAG_SCUBA_COMPETITOR_2))
	{
		applymovement(6, PacifidlogTown_Movement_CompetitorsOutOfWater)
	}
	if(flag(FLAG_SCUBA_COMPETITOR_3))
	{
		applymovement(7, PacifidlogTown_Movement_CompetitorsOutOfWater)
	}
	if(flag(FLAG_SCUBA_COMPETITOR_4))
	{
		applymovement(11, PacifidlogTown_Movement_CompetitorsOutOfWater)
	}
	waitmovement(0)
	delay(30)

	// SCORING BEGINS
	msgbox(format("Welcome back, everyone! Time for scoring! Let's see your most recent catch..."))
	closemessage
	// Buffer names of competitors to string 3 (string var 1 will be used to show the score values)
	if(flag(FLAG_SCUBA_COMPETITOR_3))
	{
		bufferstring(STR_VAR_3, "Taonga")
	}
	elif(flag(FLAG_SCUBA_COMPETITOR_4))
	{
		bufferstring(STR_VAR_3, "Rua")
	}

	// Checks score of left competitor
	applymovement(5, PacifidlogTown_Movement_JudgeChecksScoreLeft)
	waitmovement(0)
	delay(20)
	buffernumberstring(STR_VAR_1, VAR_DIVING_MINIGAME_C1_SCORE)
	bufferspeciesname(STR_VAR_2, VAR_DIVING_MINIGAME_C1_SPECIES)
	msgbox(format("{STR_VAR_3}'s catch, a {STR_VAR_2}, scored {STR_VAR_1}!"))
	closemessage
	delay(20)

	// buffer competitor names to str_var_2
	if(flag(FLAG_SCUBA_COMPETITOR_1))
	{
		bufferstring(STR_VAR_2, "Pora")
	}
	elif(flag(FLAG_SCUBA_COMPETITOR_2))
	{
		bufferstring(STR_VAR_2, "Taiko")
	}

	// Checks score of right competitor
	applymovement(5, PacifidlogTown_Movement_JudgeChecksScoreRight)
	waitmovement(0)
	delay(20)
	buffernumberstring(STR_VAR_1, VAR_DIVING_MINIGAME_C2_SCORE)
	bufferspeciesname(STR_VAR_3, VAR_DIVING_MINIGAME_C2_SPECIES)
	msgbox(format("{STR_VAR_2}'s catch, a {STR_VAR_3}, scored {STR_VAR_1}!"))
	closemessage
	delay(20)

	applymovement(5, PacifidlogTown_Movement_JudgeChecksScorePlayer)
	waitmovement(0)
	delay(20)
	buffernumberstring(STR_VAR_1, VAR_DIVING_MINIGAME_SCORE)
	bufferspeciesname(STR_VAR_2, VAR_DIVING_MINIGAME_PLAYER_SPECIES)
	if(var(VAR_DIVING_MINIGAME_SCORE) > 0)
	{
		msgbox(format("{PLAYER}'s catch, a {STR_VAR_2}, scored {STR_VAR_1}!"))
	}
	else
	{
		msgbox(format("{PLAYER}'s catch, a... huh? Looks like an unsuccessful dive! Make sure to keep track of your time down there!"))
	}
	closemessage
	delay(40)

	clearflag(FLAG_TEMP_PLAYER_WIN_SCUBA)

	// Give reward to whoever scored highest
	// Score 1 wins (person on left, boy)
	if(var(VAR_DIVING_MINIGAME_C1_SCORE) > VAR_DIVING_MINIGAME_C2_SCORE)
	{
		if(var(VAR_DIVING_MINIGAME_C1_SCORE) > VAR_DIVING_MINIGAME_SCORE)
		{
			if(flag(FLAG_SCUBA_COMPETITOR_3))
			{
				bufferstring(STR_VAR_3, "Taonga")
			}
			elif(flag(FLAG_SCUBA_COMPETITOR_4))
			{
				bufferstring(STR_VAR_3, "Rua")
			}

			msgbox(format("Looks like {STR_VAR_3} won! They'll be receiving the first place prize!"))
			closemessage
			delay(30)
			applymovement(5, Common_Movement_ExclamationMark)
			waitmovement(0)
			delay(30)
			msgbox(format("...ah, don't fret! All participants in the SCUBA SAFARI receive a little something! Here!"))
			closemessage
			randomelement(ITEM_PRETTY_FEATHER, ITEM_PEARL, ITEM_SITRUS_BERRY, ITEM_LUM_BERRY, ITEM_SILK_SCARF, ITEM_CHARCOAL, ITEM_MYSTIC_WATER, ITEM_MIRACLE_SEED, ITEM_MAGNET)
			giveitem(VAR_RESULT)
		}
		else
		{
			// if code reaches here, that means player scored higher than C1 score, and C1 is higher than C2, so player wins
			setflag(FLAG_TEMP_PLAYER_WIN_SCUBA)
		}
	}
	// Score 2 wins (person on right, girl)
	if(var(VAR_DIVING_MINIGAME_C2_SCORE) > VAR_DIVING_MINIGAME_C1_SCORE)
	{
		if(var(VAR_DIVING_MINIGAME_C2_SCORE) > VAR_DIVING_MINIGAME_SCORE)
		{
			if(flag(FLAG_SCUBA_COMPETITOR_1))
			{
				bufferstring(STR_VAR_2, "Pora")
			}
			elif(flag(FLAG_SCUBA_COMPETITOR_2))
			{
				bufferstring(STR_VAR_2, "Taiko")
			}

			msgbox(format("Looks like {STR_VAR_2} won! They'll be receiving the first place prize!"))
			closemessage
			delay(30)
			applymovement(5, Common_Movement_ExclamationMark)
			waitmovement(0)
			delay(30)
			msgbox(format("...ah, don't fret! All participants in the SCUBA SAFARI receive a little something! Here!"))
			closemessage
			randomelement(ITEM_PRETTY_FEATHER, ITEM_PEARL, ITEM_SITRUS_BERRY, ITEM_LUM_BERRY, ITEM_SILK_SCARF, ITEM_CHARCOAL, ITEM_MYSTIC_WATER, ITEM_MIRACLE_SEED, ITEM_MAGNET)
			giveitem(VAR_RESULT)
		}
		else
		{
			// if code reaches here, that means player scored higher than C2 score, and C2 is higher than C1, so player wins
			setflag(FLAG_TEMP_PLAYER_WIN_SCUBA)
		}
	}
	
	if(flag(FLAG_TEMP_PLAYER_WIN_SCUBA) || (var(VAR_DIVING_MINIGAME_SCORE) >= VAR_DIVING_MINIGAME_C1_SCORE && var(VAR_DIVING_MINIGAME_SCORE) >= VAR_DIVING_MINIGAME_C2_SCORE))
	{
		msgbox(format("Looks like {PLAYER} won! They'll be receiving the first place prize!"))
		closemessage
		giveitem(ITEM_BIG_PEARL)
		msgbox(format("All other participants will receive a smaller prize."))
		closemessage
	}

	clearflag(FLAG_TEMP_PLAYER_WIN_SCUBA)
	delay(30)

	if(flag(FLAG_NEW_DIVING_GAME_HIGH_SCORE)) // if got a new high score
	{
		clearflag(FLAG_NEW_DIVING_GAME_HIGH_SCORE)

		checkitem(ITEM_HM07)
		if(var(VAR_RESULT) == FALSE) // be given HM07 waterfall
		{
			msgbox(format("Looks like a new high score! Here's a special prize!"))
			closemessage
			giveitem(ITEM_HM07)
			msgbox(format("It says on the manual here you need all 8 GYM BADGES to use WATERFALL out of battle. Only true masters of the seas can have this HM!"))
			closemessage
		}
		else // given 5 heart scales and other good shit for new high score
		{
			msgbox(format("Looks like a new high score! Here's a special prize!"))
			closemessage
			giveitem(ITEM_HEART_SCALE, 5)
			msgbox(format("And here, take a nice rare item too!"))
			closemessage
			randomelement(ITEM_TM_DRAGON_CLAW, ITEM_TM_BULK_UP, ITEM_TM_ICE_BEAM, ITEM_TM_GIGA_DRAIN, ITEM_TM_THUNDERBOLT, ITEM_TM_EARTHQUAKE, ITEM_TM_RETURN, ITEM_TM_PSYCHIC, ITEM_TM_SHADOW_BALL, ITEM_TM_BRICK_BREAK, ITEM_TM_FLAMETHROWER, ITEM_TM57, ITEM_TM58, ITEM_EVIOLITE, ITEM_BIG_ROOT, ITEM_GRIP_CLAW, ITEM_BLACK_SLUDGE, ITEM_ROCKY_HELMET, ITEM_ZOOM_LENS, ITEM_EVIOLITE, ITEM_LEFTOVERS, ITEM_LEFTOVERS)
			giveitem(VAR_RESULT)
			msgbox(format("We find TMs, rare held items, and other stuff floating in the sea from time to time. It's important to clean up where we can!"))
			closemessage
		}
	}

	setvar(VAR_PACIFIDLOG_TOWN_STATE, 0) // reset state of diving minigame
	clearflag(FLAG_NEW_DIVING_GAME_HIGH_SCORE)
	clearflag(FLAG_DOING_DIVING_MINIGAME)

	clearflag(FLAG_SCUBA_COMPETITOR_1)
	clearflag(FLAG_SCUBA_COMPETITOR_2)
	clearflag(FLAG_SCUBA_COMPETITOR_3)
	clearflag(FLAG_SCUBA_COMPETITOR_4)

	msgbox(format("Please, come play again soon!"))
	closemessage
	delay(30)

	warpsilent(MAP_PACIFIDLOG_TOWN, 24, 14) // warpsilent player to current position, ending minigame
	releaseall
	end
}

script PacifidlogTown_EventScript_ExitDivingMinigameAllPokemonFainted
{
	lockall
	turnplayer(DIR_NORTH)
	msgbox(format("Woah there! Looks like the POKéMON down there were a bit strong! I'll heal you up."))
	closemessage
	special(HealPlayerParty)
	msgbox(format("Remember, the seas can be dangerous!\N I'd get my hands on a POKéMON with the RUN AWAY ability if I were you. They're great for getting out of rough situations!"))
	closemessage

	clearflag(FLAG_SCUBA_COMPETITOR_1)
	clearflag(FLAG_SCUBA_COMPETITOR_2)
	clearflag(FLAG_SCUBA_COMPETITOR_3)
	clearflag(FLAG_SCUBA_COMPETITOR_4)

	setvar(VAR_PACIFIDLOG_TOWN_STATE, 0) // reset state of diving minigame
	clearflag(FLAG_NEW_DIVING_GAME_HIGH_SCORE)
	clearflag(FLAG_DOING_DIVING_MINIGAME)
	turnplayer(DIR_SOUTH)
	releaseall
	end
}
