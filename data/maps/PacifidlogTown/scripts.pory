raw `
PacifidlogTown_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, PacifidlogTown_OnTransition
	map_script MAP_SCRIPT_ON_RESUME, PacifidlogTown_OnResume
	map_script MAP_SCRIPT_ON_FRAME_TABLE, PacifidlogTown_OnFrame
	.byte 0

PacifidlogTown_OnFrame:
	map_script_2 VAR_PACIFIDLOG_TOWN_STATE, 1, PacifidlogTown_EventScript_ExitDivingMinigame
	.2byte 0

PacifidlogTown_OnTransition:
	setflag FLAG_VISITED_PACIFIDLOG_TOWN
	end

PacifidlogTown_OnResume:
	setstepcallback STEP_CB_PACIFIDLOG_BRIDGE
	end

PacifidlogTown_EventScript_NinjaBoy::
	msgbox PacifidlogTown_Text_NeatHousesOnWater, MSGBOX_NPC
	end

PacifidlogTown_EventScript_Girl::
	msgbox PacifidlogTown_Text_FastRunningCurrent, MSGBOX_NPC
	end

PacifidlogTown_EventScript_Fisherman::
	msgbox PacifidlogTown_Text_SkyPillarTooScary, MSGBOX_NPC
	end

PacifidlogTown_EventScript_TownSign::
	msgbox PacifidlogTown_Text_TownSign, MSGBOX_SIGN
	end

PacifidlogTown_Text_FastRunningCurrent:
	.string "The sea between PACIFIDLOG and\n"
	.string "SLATEPORT has a fast-running tide.\p"
	.string "If you decide to SURF, you could end\n"
	.string "up swept away somewhere else.$"

PacifidlogTown_Text_NeatHousesOnWater:
	.string "See, isn't it neat?\n"
	.string "These houses are on water!\p"
	.string "I was born here!$"

PacifidlogTown_Text_SkyPillarTooScary:
	.string "The SKY PILLAR?\p"
	.string "…Oh, you must mean that tall, tall\n"
	.string "tower a little further out.\p"
	.string "If you asked me, I wouldn't climb it.\n"
	.string "It's too scary to get up that high.\p"
	.string "Life at sea level in PACIFIDLOG,\n"
	.string "that suits me fine.$"

PacifidlogTown_Text_TownSign:
	.string "PACIFIDLOG TOWN\p"
	.string "“Where the morning sun smiles upon\n"
	.string "the waters.”$"

`

script PacifidlogTown_EventScript_AppleSalesman
{
	lock
	faceplayer
	message(format("Apples for sale! Come get your apples, fresh from the mainland!"))
	waitmessage
	pokemart(PacifidlogTown_AppleMart)
	msgbox(format("Come back soon!"))
	closemessage
	release
	end
}

mart PacifidlogTown_AppleMart
{
	ITEM_SWEET_APPLE
	ITEM_TART_APPLE
	ITEM_SYRUPY_APPLE
	ITEM_FRESH_START_MOCHI
}

script PacifidlogTown_EventScript_DivingMinigameNPC
{
	lock
	faceplayer
	msgbox(format("Heya, young TRAINER! Do you want to play the PACIFIDLOG Diving Game?"), MSGBOX_YESNO)

	if(var(VAR_RESULT) == 0) // no
	{
		msgbox(format("Alright! Come back soon!"))
		closemessage
		release
		end
	}
	else
	{
		msgbox(format("Alrighty! Let's get you down there!"))
		closemessage
		setflag(FLAG_DOING_DIVING_MINIGAME)
		clearflag(FLAG_NEW_DIVING_GAME_HIGH_SCORE)

		special(EnterDivingMinigame)

		warp(MAP_DIVING_MINIGAME, 8, 12)
		waitstate
		end
	}

	release
	end
}

script PacifidlogTown_EventScript_ExitDivingMinigame
{
	lockall
	turnplayer(DIR_NORTH)

	buffernumberstring(STR_VAR_1, VAR_DIVING_MINIGAME_SCORE)

	msgbox(format("Looks good!\N Your score was {STR_VAR_1}!"))
	closemessage

	if(flag(FLAG_NEW_DIVING_GAME_HIGH_SCORE)) // if got a new high score
	{
		clearflag(FLAG_NEW_DIVING_GAME_HIGH_SCORE)

		checkitem(ITEM_HM07)
		if(var(VAR_RESULT) == FALSE) // be given HM07 waterfall
		{
			msgbox(format("Looks like a new high score! Here's a special prize!"))
			closemessage
			giveitem(ITEM_HM07)
			msgbox(format("It says on the manual here you need all 8 GYM BADGES to use WATERFALL out of battle. Only true masters of the seas can have this HM!"))
			closemessage
		}
		else // given 5 heart scales for new high score
		{
			msgbox(format("Looks like a new high score! Here's a special prize!"))
			closemessage
			giveitem(ITEM_HEART_SCALE, 5)
			msgbox(format("And here, take a nice TM too!"))
			closemessage
			randomelement(ITEM_TM_DRAGON_CLAW, ITEM_TM_BULK_UP, ITEM_TM_ICE_BEAM, ITEM_TM_GIGA_DRAIN, ITEM_TM_THUNDERBOLT, ITEM_TM_EARTHQUAKE, ITEM_TM_RETURN, ITEM_TM_PSYCHIC, ITEM_TM_SHADOW_BALL, ITEM_TM_BRICK_BREAK, ITEM_TM_FLAMETHROWER, ITEM_TM57, ITEM_TM58)
			giveitem(VAR_RESULT)
			msgbox(format("We find TMs floating in the sea from time to time. It's important to clean up where we can!"))
			closemessage
		}
	}

	setvar(VAR_PACIFIDLOG_TOWN_STATE, 0) // reset state of diving minigame
	clearflag(FLAG_NEW_DIVING_GAME_HIGH_SCORE)
	clearflag(FLAG_DOING_DIVING_MINIGAME)

	turnplayer(DIR_SOUTH) // make sure player doesn't immediately trigger dialogue again
	releaseall
	end
}
