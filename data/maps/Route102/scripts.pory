raw `
Route102_MapScripts::
	.byte 0

Route102_EventScript_LittleBoy::
	msgbox_mugshot Route102_Text_ImNotVeryTall, MSGBOX_NPC, MUGSHOT_LITTLEBOY
	end

Route102_EventScript_RouteSignOldale::
	msgbox Route102_Text_RouteSignOldale, MSGBOX_SIGN
	end

Route102_EventScript_RouteSignPetalburg::
	msgbox Route102_Text_RouteSignPetalburg, MSGBOX_SIGN
	end

Route102_EventScript_Boy::
	msgbox_mugshot Route102_Text_CatchWholeBunchOfPokemon, MSGBOX_NPC, MUGSHOT_BOY1
	end

Route102_EventScript_Calvin::
	trainerbattle_single TRAINER_CALVIN_1, Route102_Text_CalvinIntro, Route102_Text_CalvinDefeated, Route102_EventScript_CalvinRegisterMatchCallAfterBattle
	callnative ClearMugshot
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, Route102_EventScript_CalvinRematch
	setvar VAR_0x8004, TRAINER_CALVIN_1
	specialvar VAR_RESULT, IsTrainerRegistered
	goto_if_eq VAR_RESULT, FALSE, Route102_EventScript_CalvinTryRegister
	msgbox_mugshot Route102_Text_CalvinPostBattle, MSGBOX_DEFAULT, MUGSHOT_YOUNGSTER
	release
	end

Route102_EventScript_CalvinRegisterMatchCallAfterBattle::
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	goto_if_set FLAG_HAS_MATCH_CALL, Route102_EventScript_CalvinRegisterMatchCall
	release
	end

Route102_EventScript_CalvinRegisterMatchCall::
	msgbox_mugshot Route102_Text_CalvinRegisterShort, MSGBOX_DEFAULT, MUGSHOT_YOUNGSTER
	register_matchcall TRAINER_CALVIN_1
	release
	end

Route102_EventScript_CalvinTryRegister::
	goto_if_set FLAG_HAS_MATCH_CALL, Route102_EventScript_CalvinRegister
	msgbox_mugshot Route102_Text_CalvinPostBattle, MSGBOX_DEFAULT, MUGSHOT_YOUNGSTER
	release
	end

Route102_EventScript_CalvinRegister::
	msgbox Route102_Text_CalvinRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_CALVIN_1
	release
	end

Route102_EventScript_CalvinRematch::
	trainerbattle_rematch TRAINER_CALVIN_1, Route102_Text_CalvinRematchIntro, Route102_Text_CalvinRematchDefeated
	msgbox_mugshot Route102_Text_CalvinRematchPostBattle, MSGBOX_AUTOCLOSE, MUGSHOT_YOUNGSTER
	end

Route102_EventScript_Rick::
	trainerbattle_single TRAINER_RICK, Route102_Text_RickIntro, Route102_Text_RickDefeated
	msgbox_mugshot Route102_Text_RickPostBattle, MSGBOX_AUTOCLOSE, MUGSHOT_BUG_CATCHER
	end

Route102_EventScript_Tiana::
	trainerbattle_single TRAINER_TIANA, Route102_Text_TianaIntro, Route102_Text_TianaDefeated
	msgbox_mugshot Route102_Text_TianaPostBattle, MSGBOX_AUTOCLOSE, MUGSHOT_LASS
	end

Route102_EventScript_Allen::
	trainerbattle_single TRAINER_ALLEN, Route102_Text_AllenIntro, Route102_Text_AllenDefeated
	msgbox_mugshot Route102_Text_AllenPostBattle, MSGBOX_AUTOCLOSE, MUGSHOT_YOUNGSTER
	end

Route102_Text_WatchMeCatchPokemon:
	.string "WALLY: {PLAYER}…\n"
	.string "POKéMON hide in tall grass like this,\l"
	.string "don't they?\p"
	.string "Please watch me and see if I can\n"
	.string "catch one properly.\p"
	.string "…Whoa!$"

Route102_Text_WallyIDidIt:
	.string "WALLY: I did it… It's my…\n"
	.string "My POKéMON!$"

Route102_Text_LetsGoBack:
	.string "{PLAYER}, thank you!\n"
	.string "Let's go back to the GYM!$"

Route102_Text_ImNotVeryTall:
	.string "I'm…not very tall, so I sink right\n"
	.string "into tall grass.\p"
	.string "The grass goes up my nose and…\n"
	.string "Fwafwafwafwafwa…\p"
	.string "Fwatchoo!$"

Route102_Text_CatchWholeBunchOfPokemon:
	.string "I'm going to catch a whole bunch of\n"
	.string "POKéMON!$"

Route102_Text_RouteSignOldale:
	.string "ROUTE 102\n"
	.string "{RIGHT_ARROW} OLDALE TOWN$"

Route102_Text_RouteSignPetalburg:
	.string "ROUTE 102\n"
	.string "{LEFT_ARROW} PETALBURG CITY$"
`

script Route102_Pokemon_Farfetchd
{
	lock
	faceplayer
	playmoncry(SPECIES_FARFETCHD, CRY_MODE_NORMAL)
	msgbox(format("Kree!"))
	waitmoncry
	closemessage
	release
	end
}

script Route102_Pokemon_Poliwag
{
	lock
	faceplayer
	playmoncry(SPECIES_POLIWAG, CRY_MODE_NORMAL)
	msgbox(format("Blub blub blub!"))
	waitmoncry
	closemessage
	release
	end
}

script Route102_Zinnia_Trigger1
{
	setvar(VAR_TEMP_1, 1)
	goto(Route102_Zinnia)
}

script Route102_Zinnia_Trigger2
{
	setvar(VAR_TEMP_1, 2)
	goto(Route102_Zinnia)
}

script Route102_Zinnia
{
	lockall
	applymovement(12, Route102_Zinnia_FrustratedWalkInPlace)
	waitmovement(0)
	msgbox_mugshot(format("Hmm..."), MSGBOX_NPC, MUGSHOT_ZINNIA)
	closemessage
	applymovement(12, Common_Movement_ExclamationMark)
	waitmovement(0)

	if(var(VAR_TEMP_1) == 1)
	{
		applymovement(12, Route102_Zinnia_WalkToPlayer1)
	}
	else
	{
		applymovement(12, Route102_Zinnia_WalkToPlayer2)
	}

	waitmovement(0)
	msgbox_mugshot(format("YOU! You see it too, right? The berry trees looking... unusual?!\NOf course you do! The energy in this region is shifting, the plants can feel it.\N I'm not crazy!"), MSGBOX_NPC, MUGSHOT_ZINNIA)
	closemessage

	if(var(VAR_TEMP_1) == 1)
	{
		applymovement(12, Route102_Zinnia_WalkAway1)
	}
	else
	{
		applymovement(12, Route102_Zinnia_WalkAway2)
	}

	waitmovement(0)
	removeobject(12)
	setflag(FLAG_ROUTE102_ZINNIA_VANISHED)
	setvar(VAR_ZINNIA_STORY_STATE, 1)
	releaseall
	end
}

movement Route102_Zinnia_FrustratedWalkInPlace
{
	walk_in_place_up * 3
	step_end
}

movement Route102_Zinnia_WalkToPlayer1
{
	walk_left * 6
	walk_in_place_down
	step_end
}

movement Route102_Zinnia_WalkToPlayer2
{
	walk_left * 5
	walk_in_place_down
	step_end
}

movement Route102_Zinnia_WalkAway1
{
	walk_left * 8
	step_end
}

movement Route102_Zinnia_WalkAway2
{
	walk_left * 8
	step_end
}