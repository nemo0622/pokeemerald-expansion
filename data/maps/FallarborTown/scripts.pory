raw `
FallarborTown_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, FallarborTown_OnTransition
	.byte 0

FallarborTown_OnTransition:
	setflag FLAG_VISITED_FALLARBOR_TOWN
	setvar VAR_CONTEST_HALL_STATE, 0
	clearflag FLAG_CONTEST_SKETCH_CREATED
	end

FallarborTown_EventScript_ExpertM::
	lock
	faceplayer
	goto_if_set FLAG_DEFEATED_EVIL_TEAM_MT_CHIMNEY, FallarborTown_EventScript_ExpertMNormal
	msgbox FallarborTown_Text_ShadyCharactersCozmosHome, MSGBOX_DEFAULT
	release
	end

FallarborTown_EventScript_ExpertMNormal::
	msgbox FallarborTown_Text_RegionKnownForMeteors, MSGBOX_DEFAULT
	release
	end

FallarborTown_EventScript_Girl::
	msgbox FallarborTown_Text_MyPreciousAzurill, MSGBOX_NPC
	end

FallarborTown_EventScript_Gentleman::
	msgbox FallarborTown_Text_HaveYouChallengedFlannery, MSGBOX_NPC
	end

FallarborTown_EventScript_Azurill::
	lock
	faceplayer
	waitse
	playmoncry SPECIES_AZURILL, CRY_MODE_NORMAL
	msgbox FallarborTown_Text_Azurill, MSGBOX_DEFAULT
	waitmoncry
	release
	end

FallarborTown_EventScript_BattleTentSign::
	msgbox FallarborTown_Text_BattleTentSign, MSGBOX_SIGN
	end

FallarborTown_EventScript_TownSign::
	msgbox FallarborTown_Text_TownSign, MSGBOX_SIGN
	end

FallarborTown_EventScript_MoveTutorSign::
	msgbox FallarborTown_Text_MoveTutorSign, MSGBOX_SIGN
	end

FallarborTown_Text_ShadyCharactersCozmosHome:
	.string "Something's happening,\n"
	.string "and I don't like it!\p"
	.string "I've seen shady characters wandering\n"
	.string "in and out of PROF. COZMO's home…$"

FallarborTown_Text_RegionKnownForMeteors:
	.string "This region's been known for meteors\n"
	.string "since the olden days.\p"
	.string "They say METEOR FALLS was gouged out\n"
	.string "by a falling meteorite long ago.$"

FallarborTown_Text_MyPreciousAzurill:
	.string "See! Take a look!\n"
	.string "This is my precious AZURILL!\p"
	.string "It's slick and smooth and plushy, too!$"

FallarborTown_Text_Azurill:
	.string "AZURILL: Rooreelooo.$"

FallarborTown_Text_HaveYouChallengedFlannery:
	.string "Have you already challenged FLANNERY,\n"
	.string "the LEADER of LAVARIDGE GYM?\p"
	.string "The girl's grandfather was famous.\n"
	.string "He was one of the ELITE FOUR in the\l"
	.string "POKéMON LEAGUE at one point.\p"
	.string "It wouldn't surprise me to see FLANNERY\n"
	.string "become a great TRAINER in her own\l"
	.string "right.$"

FallarborTown_Text_BattleTentSign:
	.string "BATTLE TENT FALLARBOR SITE\n"
	.string "“May the Greatest Teams Gather!”$"

FallarborTown_Text_TownSign:
	.string "FALLARBOR TOWN\n"
	.string "“A farm community with small gardens.”$"

FallarborTown_Text_MoveTutorSign:
	.string "MOVE TUTOR'S HOUSE\n"
	.string "“New moves taught to POKéMON.”$"

`

script FallarborTown_EventScript_ProfElm
{
	lockall
	faceplayer

	if(!flag(FLAG_MET_PROF_ELM))
	{
		msgbox(format("Hey there! My name is Professor Elm. I'm visiting from the JOHTO region to see the local HOENN POKéMON. And you are...?"))
		closemessage
		delay(60)
		applymovement(18, Common_Movement_ExclamationMark)
		waitmovement(0)
		msgbox(format("Oh? You moved to HOENN from JOHTO? That's great! I see you're a TRAINER with a POKéDEX, so I've got a little challenge for you.\p Show me you've caught 50 POKéMON species, and I'll give you a special POKéMON from JOHTO!"))
		closemessage
		setflag(FLAG_MET_PROF_ELM)
		releaseall
		end
	}
	else
	{
		msgbox(format("How's it going, {PLAYER}? Here to try to earn that reward? Let's see your progress..."))
		setvar(VAR_0x8004, 1)
		specialvar(VAR_RESULT, ScriptGetPokedexInfo)
		copyvar(VAR_0x8009, VAR_0x8006) // num national caught
		buffernumberstring(STR_VAR_2, VAR_0x8009) // Puts # of national pokemon caught to STR_VAR_2
		msgbox(format("You've caught {STR_VAR_2} POKéMON!"))
		closemessage

		if(var(VAR_0x8009) < 50)
		{
			msgbox(format("Looks like you've got work to do!\p I want to make sure this JOHTO POKéMON goes to the right TRAINER, it's very, very special."))
			closemessage
			releaseall
			end
		}
		else
		{
			msgbox(format("Look at that, you did it! As promised, here's the JOHTO POKéMON!"))
			closemessage
			setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_PICHU_SPIKY_EARED)
			givemon(SPECIES_PICHU_SPIKY_EARED, 5)

			playfanfare(MUS_OBTAIN_ITEM)
			msgbox(format("{PLAYER} received a special Pichu!"))
			waitfanfare
			closemessage

			msgbox(format("That Pichu is special! The scientists in JOHTO believe she traveled through time with Celebi, so it has unique stats and cannot evolve.\p She's been stuck in the lab for a while, but she always seems to want out. Now, she can go on an adventure with you!\p I should probably continue my travels. See you around, {PLAYER}!"))
			closemessage
			setflag(FLAG_COMPLETED_PICHU_QUEST)
			fadescreen(FADE_TO_BLACK)
			removeobject(18)
			delay(60)
			fadescreen(FADE_FROM_BLACK)
			releaseall
			end
		}
	}

	releaseall
	end
}

script BattleTent_EventScript_BlockBattleTent
{
	lock
	faceplayer
	msgbox(format("The BATTLE TENT is currently closed for repairs. Sorry about that."))
	closemessage
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}
