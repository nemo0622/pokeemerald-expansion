mapscripts AcrisiaCity_University_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_INTRO_STATE, 1: AcrisiaCity_University_EventScript_PlayerIntro
    ]
}

// Cutscene introducing Niko and Rania, and Player chooses starter Pokémon!
script AcrisiaCity_University_EventScript_PlayerIntro
{
    setvar(VAR_INTRO_STATE, 2)
    setobjectxy(1, 10, 18)
    lockall

    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(0)

    // Enter building
    speakername("Prof. Elia")
    msgbox(format("Welcome to the Agria University! Well, one of many campus locations, but still!\p Come, let me introduce you to everyone."))
    closemessage
    applymovement(1, Common_Movement_WalkRight)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
    waitmovement(0)

    // Meet Niko
    speakername("Prof. Elia")
    msgbox(format("Now this here is-"))
    closemessage
    playse(SE_PIN)
    applymovement(3, Common_Movement_ExclamationMark) // Niko
    waitmovement(0)
    speakername("???")
    msgbox(format("WOAH WOAH WOAH!"))
    closemessage
    applymovement(3, AcrisiaCity_University_Movement_NikoStopsPlayer)
    waitmovement(0)
    speakername("???")
    msgbox(format("Come on, Professor! You know sudden movements can scare Duskull!"))
    closemessage
    speakername("Prof. Elia")
    msgbox(format("Whoops, my bad, Niko!"))
    closemessage
    applymovement(1, Common_Movement_FaceDown)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(0)
    speakername("Prof. Elia")
    msgbox(format("As I was saying, this is Niko. He's a bit protective of his work, but he's a great resource for knowledge."))
    closemessage
    applymovement(3, Common_Movement_WalkDown)
    waitmovement(0)
    applymovement(3, Common_Movement_FaceLeft)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
    waitmovement(0)
    speakername("Niko")
    msgbox(format("Sorry about the fuss, but when Duskull gets scared, it starts floating through walls and knocking over the books!\p I study how Pokémon behave in captivity. I'm learning a lot about Pokéballs and how to care for captive Pokémon. Now, back to work."))
    closemessage
    applymovement(3, AcrisiaCity_University_Movement_NikoReturnToStation)
    waitmovement(0)

    // Meet Rania
    applymovement(1, AcrisiaCity_University_Movement_ProfessorUp)
    applymovement(OBJ_EVENT_ID_PLAYER, AcrisiaCity_University_Movement_PlayerFollowProfessorUp)
    waitmovement(0)
    speakername("Prof. Elia")
    msgbox(format("This section of the building is for Rania! Here, I'll let her tell you what she does."))
    closemessage
    applymovement(4, AcrisiaCity_University_Movement_RaniaGreetsPlayer)
    waitmovement(0)
    speakername("Rania")
    msgbox(format("Yia sou! I'm Rania, and you must be... the new field assistant, {PLAYER}!\p I study the movement of Pokémon populations across the Ilios Region. Part of your fieldwork is collecting Pokémon location data, and I'm the one who goes through it all and finds trends!\p I'd love to keep chatting, but I've got a deadline to meet. Nice to meet you!"))
    closemessage
    applymovement(4, AcrisiaCity_University_Movement_RaniaReturnsToStation)
    waitmovement(0)

    // Over to Professor Elia's area
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
    waitmovement(0)
    applymovement(1, AcrisiaCity_University_Movement_ProfessorToMainPosition)
    applymovement(OBJ_EVENT_ID_PLAYER, AcrisiaCity_University_Movement_PlayerFollowProfessorToMainPosition)
    waitmovement(0)
    speakername("Prof. Elia")
    msgbox(format("And here's my area of the building! Other students come in and out over time, but they're mostly out doing fieldwork. Speaking of which..."))
    closemessage

    // Tries to find Pokédex, fails. Gets player Pokémon instead
    applymovement(1, AcrisiaCity_University_Movement_ProfessorTriesToFindDex_1)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_QuestionMark)
    applymovement(1, AcrisiaCity_University_Movement_ProfessorTriesToFindDex_2)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(0)
    speakername("Prof. Elia")
    msgbox(format("Sooo... it looks like I don't quite have an extra Pokédex on hand. I ordered a new set of lab equipment which included a new one, but it seems like that hasn't come yet..."))
    closemessage
    delay(30)
    applymovement(1, Common_Movement_ExclamationMark)
    waitmovement(0)
    speakername("Prof. Elia")
    msgbox(format("{PLAYER}! Oh, I've had the most fun idea! How about you go {COLOR GREEN}find the lost delivery{COLOR DARK_GRAY} and help them out?\p I've got a few rare Pokémon in my briefcase here. Why don't you choose one to adventure with you?"))
    closemessage

    // Give Pokémon
    callnative(StartNewPokeballCaseUI)
    waitstate

    bufferspeciesname(STR_VAR_1, VAR_STARTER_MON)
    playfanfare(MUS_OBTAIN_ITEM)
    message(format("{PLAYER} received a {STR_VAR_1}!"))
    waitmessage
    waitfanfare
    delay(20)
    speakername("Prof. Elia")
    msgbox(format("Ah, {STR_VAR_1}! That Pokémon suits you. Now, for the task at hand: the {COLOR GREEN}lost delivery{COLOR DARK_GRAY} should be {COLOR GREEN}South{COLOR DARK_GRAY} of Acrisia City. Usually, the mover that brings our shipments here stops at a {COLOR GREEN}Stable{COLOR DARK_GRAY} in the woods. Why don't you check there?"))
    closemessage

    setflag(FLAG_SYS_B_DASH)
    clearflag(FLAG_HIDE_SECOND_PROFESSOR_SPRITE)
    setflag(FLAG_HIDE_FIRST_PROFESSOR_SPRITE)
    clearflag(FLAG_HIDE_MAP_NAME_POPUP)

    warpsilent(MAP_ACRISIA_CITY_UNIVERSITY, 6, 12)
    waitstate

    // NOTES:
    // VAR_INTRO_STATE is now 2. Player has a Pokémon, and is tasked with heading South to a Stable on the road to find the delivery.
    // Some other flags, like running shoes, have also been enabled here just for convenience

    releaseall
    end

}

movement AcrisiaCity_University_Movement_NikoStopsPlayer
{
    walk_fast_left * 2
    jump_in_place_left * 2
    delay_16
    step_end
}

movement AcrisiaCity_University_Movement_NikoReturnToStation
{
    walk_right * 2
    walk_up
    face_up
    step_end
}

movement AcrisiaCity_University_Movement_ProfessorUp
{
    walk_up * 5
    face_right
    step_end
}

movement AcrisiaCity_University_Movement_PlayerFollowProfessorUp
{
    walk_up * 5
    walk_right
    face_up
    step_end
}

movement AcrisiaCity_University_Movement_RaniaGreetsPlayer
{
    walk_right
    walk_down * 3
    walk_left * 2
    face_down
    step_end
}

movement AcrisiaCity_University_Movement_RaniaReturnsToStation
{
    walk_right * 2
    walk_up * 3
    walk_left
    face_left
    step_end
}

movement AcrisiaCity_University_Movement_ProfessorToMainPosition
{
    walk_left * 5
    walk_up * 2
    face_down
    step_end
}

movement AcrisiaCity_University_Movement_PlayerFollowProfessorToMainPosition
{
    walk_left * 6
    walk_up * 1
    face_up
    step_end
}

movement AcrisiaCity_University_Movement_ProfessorTriesToFindDex_1
{
    walk_up
    walk_left
    face_left
    delay_16 * 3
    emote_question_mark
    delay_16
    walk_fast_up
    face_right
    delay_16 * 2
    emote_exclamation_mark
    delay_16
    walk_fast_down * 2
    walk_fast_right * 3
    walk_fast_up * 2
    step_end
}

movement AcrisiaCity_University_Movement_ProfessorTriesToFindDex_2
{
    walk_fast_right * 3
    jump_in_place_right * 2
    delay_16 * 3
    walk_left * 3
    walk_down * 2
    walk_left * 2
    face_down
    step_end
}

// -------------------------------------------------------------------------------------------------------------

script AcrisiaCity_University_EventScript_Duskull
{
    lock
    speakername("Duskull")
    playmoncry(SPECIES_DUSKULL, CRY_MODE_NORMAL)
    msgbox("Bwraaahhh!")
    waitmoncry
    closemessage
    release
    end
}

script AcrisiaCity_University_EventScript_Niko
{
    lock
    faceplayer
    speakername("Niko")
    msgbox(format("Come to check out my research, {PLAYER}? I'm observing the Duskull to see how it behaves.\p There are old folktales of Ghost-type Pokémon being evil, but this Duskull seems totally friendly!"))
    closemessage
    release
    end
}

script AcrisiaCity_University_EventScript_Rania
{
    lock
    faceplayer
    speakername("Rania")
    msgbox(format("Yia sou, {PLAYER}! You doin' well?\p Me? I'm stuck in here doing data analysis. Fieldwork information needs to be sorted into spreadsheets and analyzed, that's where I come in! It's sure not the most exciting part, but I find out some cool stuff."))
    closemessage
    release
    end
}

// -------------------------------------------------------------------------------------------------------------
// ---------------------------  Code called when speaking to Prof. Elia  --------------------------------
// -------------------------------------------------------------------------------------------------------------

script AcrisiaCity_EventScript_ProfessorElia
{
    lock
    faceplayer

    if(var(VAR_INTRO_STATE) == 2) // sent to get lost delivery of Pokédex
    {
        speakername("Prof. Elia")
        msgbox(format("Welcome back, {PLAYER}! Did you find that {COLOR GREEN}lost delivery{COLOR DARK_GRAY} yet?"), MSGBOX_YESNO)
        if(var(VAR_RESULT) == 0) // no
        {
            msgbox(format("Hmm, that's concerning... local crime groups have been active lately, I'm nervous that the delivery cart was attacked. Keep us updated on the situation."))
            closemessage
        }
        else
        {
            if(flag(FLAG_FOUND_LOST_DELIVERY)) // actually did!
            {
                msgbox(format("Ochi! The delivery cart was attacked!? Well I'm glad everyone is okay...\p Here, I'll enable your Pokédex."))
                closemessage
                playse(SE_CLICK)
	            delay(10)
	            playse(SE_CLICK)
	            delay (10)
	            playse(SE_CLICK)
	            delay(10)
	            playse(SE_CLICK)
	            delay(20)
                playfanfare(MUS_OBTAIN_ITEM)
                msgbox("{PLAYER}'s Pokédex was enabled!")
                waitfanfare
                setflag(FLAG_SYS_POKEDEX_GET)
                setflag(FLAG_ADVENTURE_STARTED)
                setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
                closemessage
                speakername("Prof. Elia")
                msgbox(format("I think it's a good idea to train your Pokémon for battle, in case more troublemakers come around. Why don't you {COLOR GREEN}battle the Pokémon Gyms{COLOR DARK_GRAY} you find while you're exploring the Ilios Region? That'll get your Pokémon raring to go!"))
                closemessage
                speakername("Prof. Elia")
                msgbox(format("You're all set to go, {PLAYER}! Capture Pokémon, collect data, and get stronger! Oh, and make sure to help people when you can. Good luck!"))
                closemessage
                setvar(VAR_INTRO_STATE, 3)
            }
            else
            {
                msgbox(format("You did? Well where's the Pokédex? ...Come on {PLAYER}, we need to make sure the delivery folks are alright! Go look again!"))
                closemessage
            }
        }
    }

    release
    end

}
